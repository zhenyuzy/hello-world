!******************  GEOMETRY ANALYSIS FOR THE CABLES ******************
!*  CODED BY: S.C. YANG                                                *
!*  UPDATED: 09/10/2019                                                *
!***********************************************************************

/PREP7
ESEL,S,ENAME,,LINK180
KEYOPT,2,3,0
INISTATE,SET,CSYS,-2
INISTATE,SET,DTYP,EPEL
INISTATE,DEFI,,,,,1E-2                            !INITIAL SMALL STRAIN FOR CABLE ELEMENTS
ALLSEL,ALL

/SOLU
ANTYPE,0                                !NONLINEAR STATIC ANALYSIS
NLGEOM,ON
PSTRES,ON                               !TURN ON THE LARGE DEFORMATION
EMATWRITE,YES
SSTIF,ON                                !TURN ON THE STRESS STIFFEN
NSUBST,20
OUTRES,ALL,ALL
ACEL,,,-9.81
SOLVE
FINISH

/POST1
SET,LAST,LAST                           !SET THE LAST LOAD STEP
ESEL,ALL
*GET,ENUMMAX,ELEM,0,NUM,MAX
CMSEL,S,CABLE,ELEM
*GET,ENUM,ELEM,0,COUNT
ETABLE,EPELT,LEPEL,1
*DIM,CBLSTRN,ARRAY,ENUM,2
*DO,I,1,ENUM
    CBLSTRN(I,1) = I
    *GET,CBLSTRN(I,2),ELEM,ENUMMAX-ENUM+I,ETAB,EPELT 
*ENDDO
*MWRITE,CBLSTRN,CBLSTRN,TXT,,JIK,2,ENUM,1
(2E14.6)
FINISH

/PREP7
UPGEOM,1,LAST,LAST,BRDG,RST
NSEL,ALL
*GET,NNUMMAX,NODE,0,NUM,MAX
CMSEL,S,CABLE,ELEM
NSLE
*GET,NNUM,NODE,0,COUNT
*DIM,CBLN,ARRAY,NNUM,4
*DIM,CBLE,ARRAY,ENUM,4
*DO,I,1,NNUM
    CBLN(I,1) = I
    CBLN(I,2) = NX(NNUMMAX-NNUM+I)
    CBLN(I,3) = NY(NNUMMAX-NNUM+I)
    CBLN(I,4) = NZ(NNUMMAX-NNUM+I)
*ENDDO
*DO,I,1,ENUM
    CBLE(I,1) = I
    CBLE(I,2) = NELEM(ENUMMAX-ENUM+I,1)-(NNUMMAX-NNUM)
    CBLE(I,3) = NELEM(ENUMMAX-ENUM+I,2)-(NNUMMAX-NNUM)
*ENDDO

*MWRITE,CBLN,CBLN,TXT,,JIK,4,NNUM,1
(4E14.6)
*MWRITE,CBLE,CBLE,TXT,,JIK,3,ENUM,1
(3E14.6)
ALLSEL,ALL
FINISH
